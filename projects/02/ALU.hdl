// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    // * NOTE: to negate, we do max (fully true) + x and then we increment
    // zero the x input if zx
    Mux16(a=x, b[0..15]=false, sel=zx, out=x0);
    // negate the x input if nx
    Not16(in=x0, out=negx);
    Mux16(a=x0, b=negx, sel=nx, out=inx);
    // zero the y input if yx
    Mux16(a=y, b[0..15]=false, sel=zy, out=y0);
    // negate the y input if ny
    Not16(in=y0, out=negy);
    Mux16(a=y0, b=negy, sel=ny, out=iny);
    // compute x + y
    Add16(a=inx, b=iny, out=addxy);
    // compute x and y
    And16(a=inx, b=iny, out=andxy);
    // select the operation to keep
    Mux16(a=andxy, b=addxy, sel=f, out=result);
    // negate output
    Not16(in=result, out=negr);
    Mux16(a=result, b=negr, sel=no, out[0..7]=r1, out[8..15]=r2, out[15]=ng, out=out);
    // check if output is zero
    Or8Way(in=r1, out=firsthalf0);
    Or8Way(in=r2, out=secondhalf0);
    Or(a=firsthalf0, b=secondhalf0, out=nzr);
    Not(in=nzr, out=zr);

    // check if output is negative

}